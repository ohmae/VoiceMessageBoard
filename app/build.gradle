apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-crash'
apply plugin: 'me.tatarka.retrolambda'

def applicationName = "VoiceMessageBoard"
def versionMajor = 1
def versionMinor = 1
def versionPatch = 3

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "net.mm2d.android.vmb"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        resConfigs "en", "ja"
        vectorDrawables.useSupportLibrary = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix = ".debug"
            versionNameSuffix = "d"
            manifestPlaceholders = [app_name: "Dubug"]
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [app_name: "@string/app_name"]
        }
    }
    applicationVariants.all { variant ->
        if (variant.buildType.name != "release") {
            return
        }
        variant.outputs.each { output ->
            if (output.outputFile != null && output.outputFile.name.endsWith('release.apk')) {
                def name = "${applicationName}-${versionName}.apk"
                output.outputFile = new File(output.outputFile.parent, name)
            }
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.google.firebase:firebase-core:10.0.1'
    compile 'com.google.firebase:firebase-crash:10.0.1'
    testCompile 'junit:junit:4.12'
}

setProperty("FirebaseServiceAccountFilePath",
        rootProject.rootDir.parentFile.absolutePath + "/firebase-crashreporting-vmd.json")

apply plugin: 'com.google.gms.google-services'
